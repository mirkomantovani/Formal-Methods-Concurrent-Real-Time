\begin{lstlisting}[fontadjust, mathescape, frame=single]
class RobotController
  -- RobotController handles all the actions that the robot can execute, 
  -- specifying preconditions, axioms over the duration of the action
  -- and the sequences of events that must happen during the action

  temporal domain integer;

  TD items

  predicates
    do({
    PickFromBinArea,
    DropToLocalBin,
    GoToTombstone,
    PickFromLocalBin,
    DropToTombstone,
    PickFromTombstone,
    GoToConveyorBelt,
    DropToConveyorBelt,
    GoToBinArea
    });

    isArmMoving(),
    PickFromBinAreaT0(), PickFromBinAreaT1(),
    PickFromBinAreaT2(), PickFromBinAreaT3();

  variables
    currentAction({
    PickFromBinArea,
    DropToLocalBin,
    GoToTombstone,
    PickFromLocalBin,
    DropToTombstone,
    PickFromTombstone,
    GoToConveyorBelt,
    DropToConveyorBelt,
    GoToBinArea
    }),

    -- None -> Proceed normally with the execution
    -- Emergency -> Stop the robot and wait for the HDICommand to return to None
    -- Continue -> when the robot has placed a piece on the tombstone, it needs 
    -- to wait for the lavoration to be done and for the operator to signal that 
    -- the lavoration has terminated.

    HDICommand({
    None,
    Emergency,
    Continue
    });
    
  TI items 
  
  consts MaxDropTime: integer,
       MaxPickUpTime: integer,
       MaxTravelTime: integer;

  modules RobotStatus: RobotStatus,
                Robot: RobotPositionSensor;

  
  
  axioms

  -- CORRECT FLOW OF ACTIONS

    -- the actions must be done in the correct order
    correctActionOrder:
      $(currentAction == PickFromBinArea \limply $
        $\until(currentAction == PickFromBinArea, currentAction == DropToLocalBin))
      \ \land$

      -- After having dropped a piece in the local bin, the robot either takes 
      -- another or goes to the tombstone
      $(currentAction == DropToLocalBin \limply $
        $(\until(currentAction == DropToLocalBin,currentAction == PickFromBinArea)\ ||$
        $\until(currentAction == DropToLocalBin, currentAction == GoToTombstone)))
      \ \land$

      -- After having gone to the tombstone, the robot takes the stored working 
      -- piece with the end effector
      $(currentAction == GoToTombstone \limply $
        $(\until(currentAction == GoToTombstone, currentAction == PickFromLocalBin)))
      \ \land$

      -- After having picked up a piece from the local bin, the robot drops it 
      -- into the tombstone
      $(currentAction == PickFromLocalBin \limply $
        $\until(currentAction == PickFromLocalBin, currentAction == DropToTombstone))
      \ \land$

      -- After having dropped a piece into the tombstone, the robot waits for  
      -- the operator's signal and then picks up the reshaped piece
      $(currentAction == DropToTombstone \limply $ 
        $\until(currentAction == DropToTombstone, currentAction = PickFromTombstone))
      \ \land$

      -- After having picked up the piece from the tombstone, the robot goes to
      -- the conveyor belt
      $(currentAction == PickFromTombstone \limply $
        $\until(currentAction == PickFromTombstone, currentAction = GoToConveyorBelt))
      \ \land$


      -- After having gone to the conveyor belt, the robot drops the piece onto
      -- it
      $(currentAction == GoToConveyorBelt \limply $
        $\until(currentAction == GoToConveyorBelt, currentAction == DropToConveyorBelt)
      \ \land$

      -- After having dropped the piece onto the conveyor belt, the robot either 
      -- goes back to the tombstone or it goes to the bin area if local bin is 
      -- empty
      $(currentAction == DropToConveyorBelt \limply $
        $\until(currentAction == DropToConveyorBelt, currentAction == GoToTombstone)\ ||$
        $\until(currentAction == DropToConveyorBelt, currentAction == GoToBinArea))
      \ \land$

      -- After having gone to the bin area, the robot picks up a piece from the 
      -- bin area
      $(currentAction == GoToBinArea \limply $
        $\until(currentAction == GoToBinArea, currentAction == PickFromBinArea);$

    -- the change in currentAction must be preceded by a do request
    correctInit:
      $\forall x (\becomes(currentAction == x) \iff do(x));$

    
    -- during emergency mode, the robot needs to be stopped immediately
    emergencyMode:
      $(HDICommand == Emergency) \limply $
        $(RobotStatus.TargetCartSpeed == None) \land (RobotStatus.TargetEndEffectorSpeed == None);$

    -- do is an instantaneous event (lasts only one time instant)
    instantaneousDo:
        $\forall x (do(x) \limply \  future(\neg do(x), 1));$

  -- DEFINITION OF PRECONDITIONS, AXIOMS HOLDING DURING THE ACTION 
  -- AND SEQUENCES OF EVENTS

  -- PickFromBinArea
    -- the robot has space in the local bin and is at the bin area
    prePickFromBinArea:
        $\becomes(currentAction == PickFromBinArea) \limply$
            $\neg RobotStatus.binFull() \land \neg Robot.Arm.holding() \land$
            $\forall x (Robot.Cart.position(x) \limply Grid.adjToBinArea(x));$

    -- the robot must be still
    duringPickFromBinArea:
        $currentAction == PickFromBinArea \limply RobotStatus.currentCartSpeed == None;$

    
    -- t0: Start -> end effector on top of cart, not holding
    -- t1: end effector on top of bin area, not holding
    -- t2: end effector on top of bin area, holding
    -- t3: End -> end effector on top of cart, holding
    $PickFromBinAreaT0() \iff (currentAction == PickFromBinArea \land \neg Robot.Arm.holding() \land$
        $\exists x (Robot.Cart.position(x) \land Robot.Arm.endEffector(x)))$;
    
    $PickFromBinAreaT1() \iff (currentAction == PickFromBinArea \land \neg Robot.Arm.holding() \land$
        $Robot.Arm.endEffector(LBA)))$;
    
    $PickFromBinAreaT2() \iff (currentAction == PickFromBinArea \land Robot.Arm.holding() \land$
        $Robot.Arm.endEffector(LBA)))$;
    
    $PickFromBinAreaT3() \iff (currentAction == PickFromBinArea \land Robot.Arm.holding() \land$
        $\exists x (Robot.Cart.position(x) \land Robot.Arm.endEffector(x)))$;
    
    -- to complete the PickFromBinArea action we need to have reached
    -- PickFromBinAreaT3()
    $\becomes(\neg currentAction == PickFromBinArea) \implies PickFromBinAreaT3()$;
    
    -- from T3 to T2 the only thing that changes is the position
    -- of the end effector
    $PickFromBinAreaT3() \implies \exists t (\lasttime(PickFromBinAreaT2(), t) \land$
        $\lastedOp_{ie}(currentAction == PickFromBinArea \land Robot.Arm.holding(), t))$;

    -- from T2 to T1 the only thing that changes is the fact that
    -- the end effector is holding a piece
    $PickFromBinAreaT2() \implies \exists t (\lasttime(PickFromBinAreaT1(), t) \land$
        $\lastedOp_{ie}(currentAction == PickFromBinArea \land Robot.Arm.endEffector(LBA)))$;
    
    -- from T1 to T0 the only thing that changes is the position
    -- of the end effector
    $PickFromBinAreaT1() \implies \exists t (\lasttime(PickFromBinAreaT0(), t) \land$
        $\lastedOp_{ie}(currentAction == PickFromBinArea \land \neg Robot.Arm.holding()))$;
      
  -- DropToLocalBin
    -- the robot holds a piece, the local bin isn't full and the endEffector 
    -- is on the cart
    preDropToLocalBin:
        $\becomes(currentAction == DropToLocalBin) \limply $
            $\neg RobotStatus.binFull() \land Robot.Arm.holding() \land $
            $\exists x (Robot.Cart.position(x) \land \textit{Robot.Arm.endEffector}(x));$
    
    -- the robot cannot move, the endEffector must stay in the same zone
    duringPreDropToLocalBin:
        $currentAction == DropToLocalBin \limply $
            $(RobotStatus.currentCartSpeed == None \land$
            $RobotStatus.currentEndEffectorSpeed == None);$
  
    -- there exists a time t in which the robot switches from 
    -- holding to not holding
    dropSequence:
        $\becomes(currentAction == preDropToLocalBin) \limply $
            $\exists t (0 <= t < MaxDropTime\ \land \lastsie(Robot.Arm.holding(),t)\ \land $
            $\future(\neg Robot.Arm.holding(),t)\ \land \lastsie(currentAction == DropToLocalBin, t));$
  
  -- GoToTombstone
    -- the robot's local bin is full, it isn't holding any piece and the 
    -- endEffector is on the cart
    preGoToTombstone:
        $\becomes(currentAction == GoToTombstone) \limply $
            $RobotStatus.binFull() \land \neg RobotStatus.holding();$
    
    -- in case the robot stops himself during the action of going towards the 
    -- tombstone, then the endEffector cannot move 
    -- in RobotPositionSensor we  already stated that if the cart is moving then 
    -- the endEffector must stay on top of the cart
    duringGoToTombstone:
        $currentAction == GoToTombstone \limply $
            $(RobotStatus.currentCartSpeed == None \limply$ 
                $RobotStatus.currentEndEffectorSpeed == None);$



    -- there exists a time t smaller than MaxTravelTime when the robot is in a 
    -- position adjacent to the tombstone
    goToTombstoneSequence:
        $\becomes(currentAction == GoToTombstone) \limply $
            $\exists t (0 <= t < MaxTravelTime \land$
            $\future(\exists x (Robot.Cart.position(x) \land Grid.adjToTombstone(x)),t) \land$
            $\lastsie(currentAction == GoToTombstone, t));$

  -- PickFromLocalBin
    -- the robot isn't holding any piece, the local bin isn't empty and the 
    -- endEffector is in the same position as the cart
    prePickFromLocalBin:
        $\becomes(currentAction == PickFromLocalBin) \limply $
            $\neg RobotStatus.binEmpty() \land \neg Robot.Arm.holding()\ \land $
            $\exists x (Robot.Cart.position(x) \land \textit{Robot.Arm.endEffector}(x));$
    
    -- the robot cannot move if the action is PickFromLocalBin, and also the 
    -- endEffector does not have to move
    duringPickFromLocalBin:
        $currentAction == PickFromLocalBin \limply $
            $(RobotStatus.currentCartSpeed == None\ \land$
            $RobotStatus.currentEndEffectorSpeed == None);$
  
    -- there exists a time t in which the robot switches from not holding to 
    -- holding
    pickFromLocalBinSequence:
        $\becomes(currentAction == PickFromLocalBin) \limply $
            $\exists t (0 <= t < MaxPickUpTime\ \land $
            $\lastsie(\neg Robot.Arm.holding(),t)\ \land $
            $\future(Robot.Arm.holding(),t)\ \land $
            $\lastsie(currentAction == PickFromLocalBin, t));$

  -- DropToTombstone
    -- the robot is holding a piece and it is in a position adjacent 
    -- to the tombstone
    preDropToTombstone:
        $\becomes(currentAction == DropToTombstone) \limply$
            $Robot.Arm.holding() \land \forall x (Robot.Cart.position(x) \limply Grid.adjToTombstone(x));$

    -- the robot cart isn't moving
    duringDropToTombstone:
        $currentAction == DropToTombstone \limply $
            $RobotStatus.currentCartSpeed == None;$






    -- the sequence of actions that the robot has to do before MaxDropTime 
    -- is to place the endEffector onto the tombstone, drop the piece and bring 
    -- the endEffector back in the same position as the cart
    dropToTombstoneSequence:
        $\becomes(currentAction == DropToTombstone) \limply $
            $\exists t1, t2, t3 (0 <= t1 < t2 < t3 < MaxDropTime\ \land $
            $\lastsie(Robot.Arm.holding(), t2)\ \land $
            $\future(\textit{Robot.Arm.endEffector}(L0), t1)\ \land $
            $\future(\lastsii(\neg Robot.Arm.holding(), t3), t2)\ \land $
            $\future(\exists x (Robot.Cart.position(x) \land \textit{Robot.Arm.endEffector}(x)), t3) \land $
            $\lastsie(currentAction == DropToTombstone, t3));$

  -- PickFromTombstone
    -- the robot has received the Continue HDI signal, it isn't holding anything 
    -- and it is in a position adjacent to the tombstone
    prePickFromTombstone:
        $\becomes(currentAction == PickFromTombstone) \limply $
            $HDICommand == Continue\ \land $
            $\neg Robot.Arm.holding()\ \land $
            $\forall x (Robot.Cart.position(x) \limply Grid.adjToTombstone(x));$

    -- the robot cart isn't moving
    duringPickFromTombstone:
        $currentAction == PickFromTombstone \limply RobotStatus.currentCartSpeed == None;$

    -- the sequence of actions that the robot has to do before MaxPickUpTime 
    -- is to place the endEffector onto the tombstone, pick up the piece and 
    -- bring the endEffector in the same position as the cart
    pickPickFromTombstoneSequence:
        $\becomes(currentAction == PickFromTombstone) \limply $
            $\exists t1, t2, t3 (0 <= t1 < t2 < t3 < MaxPickUpTime \ \land $
              $\lastsie(\neg Robot.Arm.holding(), t2)\ \land $
              $\future(\textit{Robot.Arm.endEffector}(L0), t1)\ \land $
              $\future(\lastsii(Robot.Arm.holding(), t3), t2)\ \land $
              $\future(\exists x (Robot.Cart.position(x) \land \textit{Robot.Arm.endEffector}(x)), t3)\ \land $
              $\lastsie(currentAction == PickFromTombstone, t3));$
    
  -- GoToConveyorBelt
    -- the robot is holding a piece
    preGoToConveyorBelt:
        $\becomes(currentAction == GoToConveyorBelt) \limply RobotStatus.holding();$
    







    -- in case the robot stops himself during the action of going towards the 
    -- conveyor belt, then the endEffector cannot move 
    -- in RobotPositionSensor we already stated that if the cart is moving then 
    -- the endEffector must stay on top of the cart
    duringGoToConveyorBelt:
        $currentAction == GoToConveyorBelt \limply $
            $((RobotStatus.currentCartSpeed == None$ 
                $\limply RobotStatus.currentEndEffectorSpeed == None )$
            $\land Robot.Arm.holding());$

    -- there exists a time t smaller than MaxTravelTime when the robot is at the 
    -- conveyor belt
    goToConveyorBeltSequence:
        $\becomes(currentAction == GoToConveyorBelt) \limply $
            $\exists t (0 <= t < MaxTravelTime \land \future(\exists x (Robot.Cart.position(x)\ \land $
              $Grid.adjToConveyorBelt(x)),t)\land \lastsie(currentAction == GoToConveyorBelt, t));$
      
  -- DropToConveyorBelt
    -- the robot is holding a piece and it is in a position adjacent to the 
    -- conveyor belt
    preDropToConveyorBelt:
      $\becomes(currentAction == DropToConveyorBelt) \limply $
        $Robot.Arm.holding() \land \forall x (Robot.Cart.position(x) \limply $
          $Grid.adjToConveyorBelt(x));$
    
    -- the robot cart isn't moving
    duringDropToConveyorBelt:
      $currentAction == DropToConveyorBelt \limply RobotStatus.currentCartSpeed == None;$

    -- the sequence of actions that the robot has to do before MaxDropTime is to 
    -- place the endEffector onto the conveyor belt, drop the piece and bring  
    -- the endEffector in the same position as the cart
    dropToConveyorBeltSequence:
        $\becomes(currentAction == DropToConveyorBelt) \limply $
            $\exists t1, t2, t3 (0 <= t1 < t2 < t3 < MaxDropTime\ \land $
              $\lastsie(Robot.Arm.holding(), t2)\ \land $
              $\future(\textit{Robot.Arm.endEffector}(LCB), t1)\ \land $
              $\future(\lastsii(\neg Robot.Arm.holding(), t3), t2)\ \land $
              $\future(\exists x (Robot.Cart.position(x) \land \textit{Robot.Arm.endEffector}(x)), t3)\ \land $
              $\lastsie(currentAction == DropToConveyorBelt, t3));$

  -- GoToBinArea
    -- the robot doesn't hold any piece and the bin is empty
    preGoToBinArea:
        $\becomes(currentAction == GoToBinArea) \limply $
            $\neg RobotStatus.holding() \land RobotStatus.binEmpty();$
    


    -- in case the robot stops himself during the action of going towards the 
    -- bin area, then the endEffector cannot move
    -- in RobotPositionSensor we already stated that if the cart is moving then 
    -- the endEffector must stay on top of the cart
    duringGoToBinArea:
        $currentAction == GoToBinArea \limply $
            $(RobotStatus.currentCartSpeed == None \limply$
                $RobotStatus.currentEndEffectorSpeed == None);$

    -- there exists a time t smaller than MaxTravelTime when the robot is in 
    -- a position adjacent to the bin area
    goToBinAreaSequence:
        $\becomes(currentAction == GoToBinArea) \limply $
            $\exists t (0 <= t < MaxTravelTime \land $
              $\future(\exists x (Robot.Cart.position(x)\ \land Grid.adjToBinArea(x)),t) \land$
              $\lastsie(currentAction == GoToBinArea, t));$
                      
    -- The robot can move the arm only when the cart is not moving
    isArmMoving: 
        $isArmMoving() \iff$
            $\neg (currentAction == GoToBinArea \lor currentAction == GoToTombstone \lor$
            $currentAction == GoToConveyorBelt);$
         
end RobotController.
\end{lstlisting}