-- This class collects all the sensor of the operator.
-- All the sensors used to define the operator position
-- in the map are imported here through the modules function.
-- 

class OperatorPositionSensor
    inherits PositionSensor
    
    visible arms, body, head;

    temporal domain integer;

    TD items
    
        -- We define the predicates that will be later passed to the
        -- controller. They represent the predicates of the operator'sensor
        -- position sensors.
        
        predicates
            -- TODO
            -- TODO Arms pu√≤ essere LBA e LCB?
            -- TODO
            arms({L0, L1, L2, L3, L4, L5, L6, L7, L8, L9,
                  L10, L11, L12, L13, L14, L15, L16, L17, L18, L19,
                  L20, L21, L22, L23, L24, L25, L26, L27, L28}),
            body({L0, L1, L2, L3, L4, L5, L6, L7, L8, L9,
                      L10, L11, L12, L13, L14, L15, L16, L17, L18, L19,
                      L20, L21, L22, L23, L24, L25, L26, L27, L28 }),
            head({L0, L1, L2, L3, L4, L5, L6, L7, L8, L9,
                      L10, L11, L12, L13, L14, L15, L16, L17, L18, L19,
                      L20, L21, L22, L23, L24, L25, L26, L27, L28});

    -- Definition of all the sensors of the operator
    modules LeftArm: OperatorArmPositionSensor,
            RightArm: OperatorArmPositionSensor,
            Body: OperatorBodyPositionSensor,
            Head: OperatorHeadPositionSensor,
            Grid: Grid;

    axioms
        -- Connect the predicates between the modules and the current class
        arms: \forall x (arms(x) \iff (LeftArm.position(x) || RightArm.position(x)));
        body: \forall x (body(x) \iff Body.position(x));
        head: \forall x (head(x) \iff Head.position(x));

        -- The head is on the body or in a close by cell
        headOnTheBody: \forall x (head(x) \implies body(x) || \exists y (body(y) && Grid.adjacent(x, y)));

        -- The arms are on the body or in a close by cell
        armsOnTheBody: \forall x (arms(x) \implies body(x) || \exists y (body(y) && Grid.adjacent(x, y)));

        -- The operator can move one area in each time instant
        movement: \forall x (body(x) \implies \exists y (Futr(body(y), 1) && (x == y || Grid.adjacent(x, y))));
end OperatorPositionSensor.